; This sourcecode published under the Terms of the General Public License.
; Written by Eric Esser, Wissenschaftszentrum Berlin, eric.esser@wzb.eu.
; Inspired by uib gmbh (www.uib.de), Thomas Besser (KIT), Detlef Krummel (GEI) and Tobias Friede (Fraunhofer)

[Actions] 
; common Values for (de)installation
include_insert "common.opsiinc"

if not(HasMinimumSpace ("%SystemDrive%", $MinimumSpace$))
  LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " on drive %SystemDrive% needed for " + $ProductId$
  isFatalError "No Space"
  ; Stop process and set installation status to failed
else
;  if FileExists("%ScriptPath%\delsub.opsiscript")
;    comment "Start uninstall sub section"
;    Sub "%ScriptPath%\delsub.opsiscript"
;  endif
   
	Message "Installing %installingProdName% (%installingProdVersion%)..."   
	comment "Start setup program"

  Set $InstallBatch$ = "%ScriptPath%\"+ $InstFile$ + " /SILENT /SP- /NOCANCEL /NORESTART /SUPPRESSMSGBOXES /NOLAUNCH"

  Winbatch_install
  Sub_check_exitcode	

	comment "Testing for installation success"
	if FileExists($PathToExe$)
		; Verifies if product version number matches installed version number
		set $FileInfoList$ = getFileInfoMap($PathToExe$)
		set $InstalledVersion$ = getValue("file version with dots", $FileInfoList$)
		; executable file version consistently has an extra zero digit at the end, therefore substring comparison
		if not (contains($InstalledVersion$, $ProductVersion$))
			logError "Fatal: Installed product version is " + $InstalledVersion$ + " but is supposed to be " + $ProductVersion$
			isFatalError
		endif

  ; Setting the desktop icon or removing it - as set in the config editor
  if ($DesktopIcon$ = "true")
    LinkFolder_create
  else
    LinkFolder_delete
  endif
	
		comment "include custom post install file"
		set $CustomPostInstall$ = getProductProperty("custom-post-install","none")
		if not ($CustomPostInstall$ = "none")
			if FileExists("%ScriptPath%\custom\" + $CustomPostInstall$)
	  			include_insert "%ScriptPath%\custom\" + $CustomPostInstall$
	  		endif
	  	endif
	else
		logError "Could not found " + $PathToExe$ + " after installation"
		isFatalError
	endif
endif

WinBatch_CloseSkype

[WinBatch_CloseSkype]
powershell.exe "Clear-Host ; Get-Process Skype | foreach {$_.kill()}"

[Winbatch_install]
$InstallBatch$
